buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "gt"
            url = "https://gregtech.overminddl1.com/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "jitpack.io"
            url = "https://jitpack.io"
        }
    }
    dependencies {
        classpath 'com.github.GTNH2:ForgeGradle:FG_1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "1.7.x-${config.mod_version}"
group= "iguanaman" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ThaumcraftMobAspects"

task buildInfo {
    def cmd = "git rev-parse --short HEAD"
    def proc = cmd.execute()
    proc.waitFor()
    if (proc.exitValue() == 0) {
        ext.revision = proc.text.trim()
    } else {
        ext.revision = "GITBORK"
    }
}

task setSnapshot << {
    if(System.getenv().DRONE_BUILD_NUMBER != null) {
        project.version = "1.7.x-" + config.mod_version + "-snapshot-" + System.getenv().DRONE_BUILD_NUMBER
    } else {
        println 'Not in Drone.io. Reverting to release buildType'
    }
}

task setDev << {
    project.version = "DEV-${project.buildInfo.revision}"
}

minecraft {
    version = "${config.mc_version}-${config.forge_version}"
    runDir = "run"
    
    replaceIn "ModInfo.java"
    replace "@MOD_VERSION@", project.version
    replace "@IS_RELEASE@", config.is_release
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
